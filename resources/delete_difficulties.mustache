{{!
@param Map<IRI, IRI> selector
@param IRI           target-graph
@param int           limit
@param int           offset
}}

PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbq: <http://mynarz.net/db-quiz/ontology/>

DELETE {
  GRAPH <{{target-graph}}> {
    ?subject dbq:difficulty ?difficulty .
    ?difficulty ?p1 ?o1 .
    ?o1 ?p2 ?o2 .
  }
}
WHERE {
  {
    SELECT ?subject
    WHERE {
      {
        SELECT DISTINCT ?subject
        WHERE {
          GRAPH <{{target-graph}}> {
            ?subject <{{selector.p}}> <{{selector.o}}> .
          }
        }
        ORDER BY ?subject
      }
    }
    LIMIT {{limit}}
    OFFSET {{offset}}
  }
  GRAPH <{{target-graph}}> {
    ?subject dbq:difficulty ?difficulty .
    ?difficulty dbq:forSelector [ <{{selector.p}}> <{{selector.o}}> ] ;
      ?p1 ?o1 .
    OPTIONAL {
      ?o1 ?p2 ?o2 .
    }
  }
}
