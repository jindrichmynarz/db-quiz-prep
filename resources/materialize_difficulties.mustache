{{!
@param Map<IRI,IRI> selector
@param IRI          target-graph
@param float        maximum-indegree
@param float        minimum-indegree
@param int          limit
@param int          offset
}}

PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbq: <http://mynarz.net/db-quiz/ontology/>

INSERT {
  GRAPH <{{target-graph}}> {
    ?subject dbq:difficulty [
      dbq:forSelector [ <{{selector.p}}> <{{selector.o}}> ] ;
      dbq:difficultyLevel ?difficulty
    ] .
  }
}
WHERE {
  {
    SELECT ?subject
           (IF(?indegree > {{minimum-indegree}} && ?indegree < {{maximum-indegree}}, 1,
            IF(?indegree > {{maximum-indegree}}, 0, 2)) AS ?difficulty)
    WHERE {
      {
        SELECT ?subject
        WHERE {
          {
            SELECT ?subject
            WHERE {
              GRAPH <{{target-graph}}> {
                ?subject <{{selector.p}}> <{{selector.o}}> . 
              }
            }
            ORDER BY ?subject
          }
        }
        LIMIT {{limit}}
        OFFSET {{offset}}
      }
      GRAPH <{{target-graph}}> {
        ?subject dbo:wikiPageInDegree ?indegree .
      }
    }
  }
}
